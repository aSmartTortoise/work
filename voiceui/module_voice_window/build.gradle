plugins {
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

def android_config = rootProject.ext.android
def depLibs = rootProject.ext.depsLibs

android {
    namespace 'com.voyah.window'
    compileSdk android_config.compileSdk

    defaultConfig {
        minSdk android_config.minSdk
        targetSdk android_config.targetSdk

        if (isModule.toBoolean()) {
            applicationId "com.voyah.window"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    sourceSets {
        main {
            // 独立调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

}

dependencies {

    implementation depLibs.coreKtx
    implementation depLibs.appcompat
    implementation depLibs.material

    testImplementation depLibs.junit
    androidTestImplementation depLibs.extJunit
    androidTestImplementation depLibs.espressoCore
    implementation project(path: ':lib_framework')
    implementation project(path: ':easyfloat')
    implementation project(path: ':lib_floating_window')

    kapt depLibs.arouterCompiler
    annotationProcessor 'com.github.hufeiyang.Android-AppLifecycleMgr:applifecycle-compiler:1.0.4'

    implementation depLibs.pag

}